plugins {
    id 'com.gradleup.shadow' version '9.0.0'
}

configurations {
    compileClasspath {
        resolutionStrategy {
            force 'io.papermc.paper:paper-api:1.21.9-R0.1-SNAPSHOT'
        }
    }
}

dependencies {
    implementation project(':bridge')
    implementation project(':utils')
    implementation project(':paper')
    
    compileOnly 'io.papermc.paper:paper-api:1.21.9-R0.1-SNAPSHOT'
    compileOnly 'com.zaxxer:HikariCP:6.0.0'
    compileOnly 'com.mojang:datafixerupper:8.0.16'
    compileOnly 'org.xerial:sqlite-jdbc:3.47.0.0'
    compileOnly 'it.unimi.dsi:fastutil-core:8.5.13'

    compileOnly fileTree(dir: "${rootProject.projectDir}/libs", include: [
        "BeastTokens.jar",
        "SCore.jar"
    ])

    implementation 'redis.clients:jedis:5.1.4'
    implementation 'org.apache.commons:commons-pool2:2.11.1'
    implementation 'com.tcoded:FoliaLib:0.5.1'
    compileOnly 'net.milkbowl.vault:VaultUnlockedAPI:2.15'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'org.geysermc.floodgate:api:2.2.0-SNAPSHOT'

    compileOnly 'org.black_ixx:playerpoints:3.2.6'
    compileOnly 'com.bencodez:votingplugin:6.19'
    compileOnly 'com.magmaguy:EliteMobs:9.1.9'
    compileOnly 'su.nightexpress.coinsengine:CoinsEngine:2.5.0'
    compileOnly 'me.TechsCode:UltraEconomyAPI:1.1.2'
    compileOnly 'net.Indyuce:MMOItems-API:6.9.5-SNAPSHOT'
    compileOnly 'io.lumine:MythicLib-dist:1.6.2-SNAPSHOT'
    compileOnly 'io.th0rgal:oraxen:1.163.0'
    compileOnly 'com.github.LoneDev6:api-itemsadder:3.6.2-beta-r3-b'
    compileOnly 'com.nexomc:nexo:0.9.0'
    compileOnly 'su.nightexpress.excellentcrates:ExcellentCrates:6.4.1'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching(['**/*.yml', '**/*.yaml', '**/*.properties']) {
        expand(project.properties)
    }
}

shadowJar {
    archiveFileName = "${rootProject.name}-${version}.jar"
    destinationDirectory = file("${rootProject.projectDir}/target")

    relocate 'com.tcoded.folialib', 'su.nightexpress.nightcore.lib.folialib'
    relocate 'redis.clients', 'su.nightexpress.nightcore.lib.redis'
    relocate 'org.apache.commons.pool2', 'su.nightexpress.nightcore.lib.commons.pool2'

    // Include only nightcore modules and FoliaLib
    dependencies {
        include(project(':bridge'))
        include(project(':utils'))
        include(project(':paper'))
        include(dependency('com.tcoded:FoliaLib:.*'))
        include(dependency('redis.clients:jedis:.*'))
        include(dependency('org.apache.commons:commons-pool2:.*'))
    }
}

build {
    dependsOn shadowJar
}
